// Script for build javadoc and jsdoc

def currentYear = new Date().format("yyyy")
def javadocClassPath = files(
		"$rootDir/dist/lib/zcommons-el.jar",
		"$rootDir/dist/lib/ext/slf4j-api.jar",
		"$rootDir/dist/lib/ext/servlet-api.jar",
		"$rootDir/dist/lib/ext/jython.jar",
		"$rootDir/dist/lib/ext/js.jar",
		"$rootDir/dist/lib/ext/jruby.jar",
		"$rootDir/dist/lib/ext/bsh.jar",
		"$rootDir/dist/lib/ext/groovy.jar",
		"$rootDir/dist/lib/ext/portlet-api.jar",
		"$rootDir/dist/lib/ext/closure-compiler.jar",
		"$rootDir/dist/lib/ext/commons-io.jar",
		"$rootDir/dist/lib/ext/hibernate-core.jar",
		"$rootDir/dist/lib/ext/commons-fileupload.jar",
		"$rootDir/dist/lib/ext/zsoup.jar",
		"$rootDir/dist/lib/ext/spring-core-5.2.15.RELEASE.jar",
		"$rootDir/dist/lib/ext/spring-beans-5.2.15.RELEASE.jar",
		"$rootDir/dist/lib/ext/spring-context-5.2.15.RELEASE.jar",
		"$rootDir/dist/lib/ext/spring-web-5.2.15.RELEASE.jar",
		"$rootDir/dist/lib/ext/javax.inject-1.jar",
		"$rootDir/dist/lib/ext/javaee-api.jar",
		"$rootDir/dist/lib/ext/javassist.jar",
		"$rootDir/dist/lib/ext/jfreechart.jar",
		"$rootDir/dist/lib/ext/commons-el.jar",
		"$rootDir/dist/lib/ext/jackson-databind.jar",
		"$rootDir/dist/lib/ext/jasperreports.jar",
		"$rootDir/dist/lib/ext/acegi-security.jar",
		"$rootDir/dist/lib/ext/s2-framework.jar",
		"$rootDir/dist/lib/ext/ognl.jar",
		"$rootDir/dist/lib/ext/mvel.jar",
		"$rootDir/dist/lib/ext/jcommon.jar",
		"$rootDir/dist/lib/ext/jackson-core.jar",
		"$rootDir/dist/lib/ext/Filters.jar"
)
// general javadoc options
def javadocOptions = {
	options.author true
	options.use true
	options.version true
	options.showFromProtected()
	options.addStringOption("charset", "UTF-8")
	options.addBooleanOption("notimestamp", false)
	options.addBooleanOption("-allow-script-in-comments", true)
	options.addBooleanOption('Xdoclint:none', true)
	options.addBooleanOption("breakiterator", true)
	options.addStringOption("-source ${version}")
}
def footerMessage = '<p class="legalCopy"><small>Copyright &copy; 2005-' + "${currentYear}" + ' Potix Corporation. All Rights Reserved.\n' +
		'<a href="https://sourceforge.net"><img src="https://sourceforge.net/sflogo.php?group_id=152762&amp;type=1" width="88" height="31" border="0" alt="SourceForge.net Logo" /></a><script type="text/javascript">if (location.href.indexOf(\'zkoss.org\') >= 0) {var gaJsHost = (("https:" == document.location.protocol) ? "https://ssl." : "http://www.");document.write(unescape("%3Cscript src=\'" + gaJsHost + "google-analytics.com/ga.js\' type=\'text/javascript\'%3E%3C/script%3E"));}</script><script type="text/javascript">if (location.href.indexOf(\'zkoss.org\') >= 0) {var pageTracker = _gat._getTracker("UA-121377-3");pageTracker._setDomainName("zkoss.org");pageTracker._initData();pageTracker._trackPageview();}</script>\n' +
		'\t </small></p>\n'

def addJavadocFooter(dir, footerMessage) {
	fileTree(dir: dir).matching {
		include "**/*.html"
	}.each { htmlFile ->
		ant.replaceregexp(
				file: htmlFile,
				match: '<!-- ======== END OF BOTTOM NAVBAR ======= -->\n',
				replace: '<!-- ======== END OF BOTTOM NAVBAR ======= -->\n' + footerMessage
		)
	}
}

// generate javadoc
task alljavadoc(type: Javadoc) {
	title = "$project.description ${version} API"
	options.setWindowTitle("$project.description ${version} API")
	configure javadocOptions
	source zkprojects.values().collect { dir ->
		"$dir/src/main/java"
	}
	// Can't get package.html into javadoc with gradle
	exclude '**/package.html',
			'**/ELParser.html',
			'**/*.properties',
			'**/*.jjt'
	destinationDir = file("$buildDir/docs/javadoc/zk")
	classpath = javadocClassPath

	doLast {
		addJavadocFooter("$buildDir/docs/javadoc/zk", footerMessage)

		// copy xml and html to the destination since they might be referenced by package.html
		subprojects {
			copy {
				from "$rootDir/$project.name/src/main/java"
				include "org/zkoss/**/*.xml"
				include "org/zkoss/**/*.html"
				exclude "**/package.html"
				into file("$buildDir/docs/javadoc/zk")
				setDuplicatesStrategy(DuplicatesStrategy.INCLUDE)
			}
		}
	}
}

// generate jsdoc
task alljsdoc(type: Javadoc) {
	doFirst {
		// build java for jsdoc
		def jsdocprj = ['zk', 'zkbind', 'zul', 'zkex', 'zkmax']
		jsdocprj.each {prj ->
			exec {
				if (prj == 'zkex' || prj == 'zkmax')
					workingDir "$rootDir/../zkcml/$prj"
				else
					workingDir "$rootDir/$prj"
				commandLine "$rootDir/bin/jsdoc", prj, "$rootDir"
			}
		}
	}
	title = "ZK Client Engine ${version} API"
	options.setWindowTitle("ZK Client Engine ${version} API")
	configure javadocOptions
	source files("$rootDir/jsdoc/src", "$rootDir/jsdoc/codegen")
	// Can't get package.html into javadoc with gradle
	exclude '**/package.html'
	destinationDir = file("$buildDir/docs/javadoc/jsdoc")
	classpath = files("$rootDir/jsdoc/src")

	doLast {
		addJavadocFooter("$buildDir/docs/javadoc/jsdoc", footerMessage)
	}
}

subprojects {
	// build javadoc each subproject for bundle
	task buildJavadoc(type: Javadoc) {
		// dependsOn all subprojects jar for javadoc classpath
		dependsOn rootProject.subprojects.collect { ":$it.name:jar" }
		dependsOn gradle.includedBuild('zkcml').task(':jar')
		title = "$project.description ${version} API"
		options.setWindowTitle("$project.description ${version} API")
		configure javadocOptions
		options.links 'https://docs.oracle.com/javase/8/docs/api/'
		source "$projectDir/src/main/java"

		// Can't get package.html into javadoc with gradle
		exclude '**/package.html',
				'**/ELParser.html',
				'**/*.properties',
				'**/*.jjt'
		destinationDir = file("$buildDir/docs/javadoc")

		// add jars in dist into classpath
		classpath += javadocClassPath
		// add jars in other projects(zk & cml) into classpath
		classpath += files(zkprojects.collect {prj, dir ->
			"$dir/build/libs/$prj-${version}.jar"
		})

		doLast {
			def shortFooterMessage = '<p class="legalCopy"><small>Copyright &#169; ' + "${currentYear}." + ' All rights reserved.</small></p>\n'
			addJavadocFooter("$buildDir/docs/javadoc", shortFooterMessage)
		}
	}
}